extends Node2D
class_name Spawner

signal on_wave_completed

@export var spawn_area_size: Vector2 = Vector2(1000,500)
@export var wave_data:Array[WaveData]
@export var enemy_collection:Array[UnitStats]

@onready var spawn_timer: Timer = $SpawnTimer
@onready var wave_timer: Timer = $WaveTimer

var wave_index: int = 1
var current_wave_data: WaveData
var spawned_enemies: Array[Enemy] = []

func get_random_spawn_position() -> Vector2: #随机生成位置
	var random_x: float = randf_range(-spawn_area_size.x, spawn_area_size.x)
	var random_y: float = randf_range(-spawn_area_size.y, spawn_area_size.y) 
	return Vector2(random_x, random_y)

func find_wave_data() -> WaveData: #
	for wave in wave_data:
		if wave and wave.is_valid_index(wave_index):
			return wave
	return null	

func start_wave() -> void:
	current_wave_data = find_wave_data()
	if not current_wave_data:
		printerr("No valid wave.")
		spawn_timer.stop()
		wave_timer.stop()
		return
		
	wave_timer.wait_time = current_wave_data.wave_time
	wave_timer.start()
	
	set_spawn_timer()
	
func set_spawn_timer() -> void: #根据类型设置 刷怪间隔计时器
	match current_wave_data.spawn_type:
		WaveData.SpawnType.FIXED:
			spawn_timer.wait_time = current_wave_data.fixed_spawn_time
		WaveData.SpawnType.RANDOM:
			spawn_timer.wait_time = randf_range(current_wave_data.min_spawn_time, current_wave_data.max_spawn_time)
	
	if spawn_timer.is_stopped():
		spawn_timer.start()
		
func spawn_enemy() -> void:
	var enemy_scene:= current_wave_data.get_random_unit_scene() as PackedScene #按权重随机生成敌人
	if enemy_scene:
		var spawn_pos: Vector2 = get_random_spawn_position() #随机位置
		
		var spawn_anim: = Global.SPAWN_EFFECT_SCENE.instantiate()
		get_parent().add_child(spawn_anim)
		spawn_anim.global_position = spawn_pos
		await spawn_anim.anim_player.animation_finished
		spawn_anim.queue_free()
		
		var enemy_instance:= enemy_scene.instantiate() as Enemy
		enemy_instance.global_position = spawn_pos
		get_parent().add_child(enemy_instance)
		spawned_enemies.append(enemy_instance)
	
	set_spawn_timer()

func updata_enemies_new_wave() -> void:
	for stat: UnitStats in enemy_collection:
		stat.health += stat.health_increase_per_wave
		stat.damage += stat.damage_increase_per_wave

func clear_enemies() -> void:
	if spawned_enemies.size() > 0:
		for enemy: Enemy in spawned_enemies:
			if is_instance_valid(enemy):
				enemy.destroy_enemies()
		
		spawned_enemies.clear()

func get_wave_text() -> String:
	return "Wave %s" % wave_index

func get_wave_timer_text() -> String:
	return str(max(0,int(wave_timer.time_left)))

func _on_spawn_timer_timeout() -> void:
	if not current_wave_data or wave_timer.is_stopped():
		spawn_timer.stop()
		return 
	spawn_enemy()

func _on_wave_timer_timeout() -> void:
	on_wave_completed.emit()
	spawn_timer.stop()
	clear_enemies()
	Global.get_harvesting_coins()
	Global.game_paused = true
	updata_enemies_new_wave()
